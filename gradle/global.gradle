tasks.withType(Javadoc) {
    source = sourceSets.main.java

    options {
        encoding = 'UTF-8'
        /*
        links = [
                "https://docs.oracle.com/javase/8/docs/api/",
                "https://docs.oracle.com/javaee/7/api/"
        ]
        */
    }
}

sourceSets {
    main.java.srcDirs = files("$parent.projectDir/src/$artifactName/main/java")
    main.resources.srcDirs = files("$parent.projectDir/src/$artifactName/main/resources")
    test.java.srcDirs = files("$parent.projectDir/src/$artifactName/test/java")
    test.resources.srcDirs = files("$parent.projectDir/src/$artifactName/test/resources")
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allJava

    try {
        archiveClassifier.set 'sources'
    } catch (MissingPropertyException ignored) {
        classifier = "sources"
    }
}

task javadocJar(type: Jar) {
    dependsOn 'javadoc'

    from javadoc.destinationDir

    try {
        archiveClassifier.set 'javadoc'
    } catch (MissingPropertyException ignored) {
        classifier = "javadoc"
    }
}

compileJava.options.encoding = 'UTF-8'

repositories {
    mavenLocal()
    maven {
        url "https://maven.jetbrains.space/comroid/release"
        credentials.username "Anonymous.User"
        credentials.password "anonymous"
    }
    maven {
        url "https://maven.jetbrains.space/comroid/repo"
        credentials.username "Anonymous.User"
        credentials.password "anonymous"
    }
    jcenter()
    maven { url 'https://www.jitpack.io' }
}

dependencies {
    if (project.name != 'interfaces') {
        if (isAutoDeploy) {
            api 'com.github.comroid-git:api:master-SNAPSHOT'
        } else {
            api project(':api')
        }
    }

    implementation 'com.google.flogger:flogger:0.5.1'

    compileOnly 'org.jetbrains:annotations:19.0.0'

    testImplementation 'junit:junit:4.13'
    testImplementation 'org.easymock:easymock:4.1'
    testImplementation 'org.codehaus.groovy:groovy:3.0.0-rc-2'
    testImplementation 'com.google.flogger:flogger-system-backend:0.5.1'
}
