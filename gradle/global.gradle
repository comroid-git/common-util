tasks.withType(Javadoc) {
    source = sourceSets.main.java

    options {
        encoding = 'UTF-8'
        /*
        links = [
                "https://docs.oracle.com/javase/8/docs/api/",
                "https://docs.oracle.com/javaee/7/api/"
        ]
        */
    }
}

sourceSets {
    main.java.srcDirs = files("$parent.projectDir/src/$artifactName/main/java")
    main.resources.srcDirs = files("$parent.projectDir/src/$artifactName/main/resources")
    test.java.srcDirs = files("$parent.projectDir/src/$artifactName/test/java")
    test.resources.srcDirs = files("$parent.projectDir/src/$artifactName/test/resources")
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allJava

    try {
        archiveClassifier.set 'sources'
    } catch (MissingPropertyException ignored) {
        classifier = "sources"
    }
}

task javadocJar(type: Jar) {
    dependsOn 'javadoc'

    from javadoc.destinationDir

    try {
        archiveClassifier.set 'javadoc'
    } catch (MissingPropertyException ignored) {
        classifier = "javadoc"
    }
}

compileJava.options.encoding = 'UTF-8'

repositories {
    maven { url 'https://maven.comroid.org' }
    mavenLocal()
    jcenter()
    maven { url 'https://www.jitpack.io' }
}

dependencies {
    if (isAutoDeploy) {
        api 'org.comroid:api:0.+'
        api 'org.comroid:guardian-core:0.+'
    } else {
        api project(':api')
        api project(":guardian:core")
    }

    // logging dependencies
    implementation 'org.apache.logging.log4j:log4j-api:2.11.0'
    testImplementation 'org.apache.logging.log4j:log4j-core:2.11.0'

    compileOnly 'org.jetbrains:annotations:19.0.0'

    testImplementation 'junit:junit:4.13'
    testImplementation 'org.easymock:easymock:4.1'
    testImplementation 'org.codehaus.groovy:groovy:3.0.0-rc-2'
}
