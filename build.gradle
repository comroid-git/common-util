apply plugin: 'java-library'

group 'org.comroid'

tasks.findByName('wrapper').configure {
    gradleVersion = '5.6'
    distributionUrl = "https://services.gradle.org/distributions/gradle-$gradleVersion-all.zip"
}

allprojects {
    apply plugin: 'java-library'

    group rootProject.group

    apply from: "$rootDir/gradle/vars.gradle"
    
    tasks.withType(Javadoc) {
        source = sourceSets.main.java

        options {
            encoding = 'UTF-8'
            /*
            links = [
                    "https://docs.oracle.com/javase/8/docs/api/",
                    "https://docs.oracle.com/javaee/7/api/"
            ]
            */
        }
    }

    task sourcesJar(type: Jar) {
        from sourceSets.main.allJava

        try {
            archiveClassifier.set 'sources'
        } catch (MissingPropertyException ignored) {
            classifier = "sources"
        }
    }

    task javadocJar(type: Jar) {
        dependsOn 'javadoc'

        from javadoc.destinationDir

        try {
            archiveClassifier.set 'javadoc'
        } catch (MissingPropertyException ignored) {
            classifier = "javadoc"
        }
    }

    compileJava.options.encoding = 'UTF-8'
    repositories.jcenter()

    dependencies {
        implementation 'com.google.flogger:flogger:0.4'

        compileOnly 'org.jetbrains:annotations:18.0.0'

        runtimeOnly 'com.google.flogger:flogger-system-backend:0.4'

        testImplementation 'junit:junit:4.12'
        testImplementation 'org.easymock:easymock:4.1'
        testCompileOnly 'org.codehaus.groovy:groovy:3.0.0-rc-2'
    }
}

// root project
version '1.2-SNAPSHOT'

sourceCompatibility = 1.8
targetCompatibility = 1.8

ext {
    projectDescription = "Commonly used Interfaces and Classes"
}

project(":trie") {
    version = '1.0.1-SNAPSHOT'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    ext {
        projectDescription = "Trie Map implementation for Java 8"
    }
}

project(":spellbind") {
    version = '0.10.1-SNAPSHOT'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    ext {
        projectDescription = "Blackmagic Proxies"
    }

    dependencies {
        implementation project(":trie")
    }
}

project(":upd8r-nginx") {
    version = '0.2.0'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    ext {
        projectDescription = "Update Handler using NGinX file hosts"
    }
    
    dependencies {
        api project(":")
        implementation 'com.alibaba:fastjson:1.2.62'
        implementation 'com.squareup.okhttp3:okhttp:3.14.7'
    }
}

task allJavadoc(type: Javadoc) {
    source exportedProjects.collect { //noinspection GroovyAssignabilityCheck
        project(it).sourceSets.main.allJava
    }
    classpath = files(exportedProjects.collect { //noinspection GroovyAssignabilityCheck
        project(it).sourceSets.main.compileClasspath
    })
    options.encoding = 'UTF-8'
    options.destinationDirectory(file("./docs/"))
}

subprojects {
    apply from: "$rootDir/gradle/vars.gradle"
}

apply from: 'gradle/publishing.gradle'
