version = '0.15.0'

sourceCompatibility = 1.8
targetCompatibility = 1.8

ext {
    projectDescription = "Java Uniform Adapters"
}

configurations {
    main
    java8compat.extendsFrom main
    java11compat.extendsFrom main
}

sourceSets {
    java8compat.java.srcDirs = files("$rootDir/src/uniform/main/java8compat")
    java11compat.java.srcDirs = files("$rootDir/src/uniform/main/java11compat")
}

compileJava8compatJava {
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
}

compileJava11compatJava {
    sourceCompatibility = 11
    targetCompatibility = 11
}

jar {
    dependsOn = ['compileJava8compatJava', 'compileJava11compatJava']
    tasks.findByName('compileJava8compatJava').mustRunAfter('compileJava')
    tasks.findByName('compileJava11compatJava').mustRunAfter('compileJava')

    from files(tasks.findByName('compileJava').destinationDir,
            tasks.findByName('compileJava8compatJava').destinationDir,
            tasks.findByName('compileJava11compatJava').destinationDir)

    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

dependencies {
    api project(':common-util')
    java8compatCompileOnly project(':restless')
    java11compatCompileOnly project(':restless')

    // websocket dependencies
    compileOnly project(':listnr')
    java8compatCompileOnly project(':listnr')
    java11compatCompileOnly project(':listnr')
    compileOnly project(':dreadpool')
    java8compatCompileOnly project(':dreadpool')
    java11compatCompileOnly project(':dreadpool')

    // http adapter dependencies
    java8compatCompileOnly 'com.squareup.okhttp3:okhttp:4.5.0'

    implementation 'com.google.flogger:flogger:0.5.1'
    java8compatImplementation 'com.google.flogger:flogger:0.5.1'
    java11compatImplementation 'com.google.flogger:flogger:0.5.1'

    testRuntimeOnly project(':listnr')
    testRuntimeOnly project(':dreadpool')
    testRuntimeOnly 'com.squareup.okhttp3:okhttp:4.5.0'
}

apply from: "$rootDir/gradle/publishing.gradle"

