version = '0.15.2'

sourceCompatibility = 1.8
targetCompatibility = 1.8

ext {
    projectDescription = "Java Uniform Adapters"
}

configurations {
    main
    java8compat.extendsFrom main
    java11compat.extendsFrom main
}

sourceSets {
    java8compat.java.srcDirs = files("$rootDir/src/$artifactName/main/java8compat")
    java11compat.java.srcDirs = files("$rootDir/src/$artifactName/main/java11compat")
}

compileJava8compatJava {
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
}

compileJava11compatJava {
    sourceCompatibility = 11
    targetCompatibility = 11
}

jar {
    dependsOn = ['compileJava8compatJava', 'compileJava11compatJava']
    tasks.findByName('compileJava8compatJava').mustRunAfter('compileJava')
    tasks.findByName('compileJava11compatJava').mustRunAfter('compileJava')

    from files(tasks.findByName('compileJava').destinationDir,
            tasks.findByName('compileJava8compatJava').destinationDir,
            tasks.findByName('compileJava11compatJava').destinationDir)

    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

dependencies {
    java8compatCompileOnly 'org.jetbrains:annotations:19.0.0'
    java11compatCompileOnly 'org.jetbrains:annotations:19.0.0'

    api project(':common-util')
    java8compatCompileOnly project(':restless')
    java11compatCompileOnly project(':restless')

    api project(':uniform')
    java8compatImplementation project(':uniform')
    java11compatImplementation project(':uniform')
    api project(':varbind')
    java8compatImplementation project(':varbind')
    java11compatImplementation project(':varbind')

    // websocket dependencies
    compileOnly project(':listnr')
    java8compatCompileOnly project(':listnr')
    java11compatCompileOnly project(':listnr')
    compileOnly project(':dreadpool')
    java8compatCompileOnly project(':dreadpool')
    java11compatCompileOnly project(':dreadpool')

    // http adapter dependencies
    java8compatCompileOnly 'com.squareup.okhttp3:okhttp:4.7.2'

    implementation 'com.google.flogger:flogger:0.5.1'
    java8compatImplementation 'com.google.flogger:flogger:0.5.1'
    java11compatImplementation 'com.google.flogger:flogger:0.5.1'

    testImplementation sourceSets.java8compat.output.classesDirs
    testImplementation sourceSets.java11compat.output.classesDirs
    testRuntimeOnly project(':listnr')
    testRuntimeOnly project(':dreadpool')
    testRuntimeOnly 'com.squareup.okhttp3:okhttp:4.5.0'
    testImplementation 'com.alibaba:fastjson:1.2.71'
}

apply from: "$rootDir/gradle/publishing.gradle"

